!> Extension to JOREK of the penning test case.
!> Use here the parameters defined in [[mod_penning_case]] and project them onto a grid
!> to test the accuracy of the pusher in JOREK fields.
module mod_penning_case_jorek
  use mod_project_particles
  use mod_penning_case, only: charge, mass, omega_e, omega_b, epsilon
  use data_structure, only: type_node_list, type_element_list, type_SP_MATRIX
  use mod_parameters, only: n_degrees
  implicit none
  private
  public jorek_penning_fields
contains

subroutine jorek_penning_fields(node_list, element_list, apply_dirichlet_in, &
rank_in, master_in, ifail_out)
  use mod_project_particles, only             : prepare_mumps_par_n0
  use projection_helpers,   only              : calc_rhs_f
  use mod_projection_helpers_test_tools, only : broadcast_dmumps_project_struct
  use mod_projection_helpers_test_tools, only : map_matrix_to_MUMPS_datastructure
  use phys_module, only       : F0, central_mass, central_density, tstep
  use constants, only         : mass_proton, mu_zero, el_chg, atomic_mass_unit
  use mpi_mod
  implicit none

  include 'dmumps_struc.h'        ! MUMPS include files defining its datastructure

  type(type_node_list),    intent(inout) :: node_list
  type(type_element_list), intent(inout) :: element_list
  integer,                 intent(in),optional :: rank_in,master_in
  logical,                 intent(in),optional :: apply_dirichlet_in
  integer,                 intent(out),optional :: ifail_out 

  real*8              :: area, volume
  real*8, allocatable :: integral_weights(:)
  type(DMUMPS_STRUC)  :: p
  integer             :: i, k, index
  real*8              :: t_norm, qom, B0, Phi0
  integer             :: mpi_comm_n, mpi_comm_master, i_tor_local, n_tor_local, ierr
  logical             :: apply_dirichlet
  integer             :: rank,master,ifail

  type (type_SP_MATRIX) :: a_mat

  !> Note that we have to cheat a little bit here:
  !> We need F0 nonzero for the electric potential, but we need F0 = 0 to not have
  !> a toroidal magnetic field. Set F0 to a very small number, and the potential
  !> to a very high number to get an asymptotically correct answer (at the expense
  !> of accuracy in the field representation of U due to floating-point accuracy)
  real*8, parameter :: F0_CHEAT_FACTOR = 1d-30

  !> deactivate dirichlet bnd if requires
  apply_dirichlet = .true.
  if(present(apply_dirichlet_in))  apply_dirichlet = apply_dirichlet_in

  rank = 0;   if(present(rank_in))   rank   = rank_in
  master = 0; if(present(master_in)) master = master_in

  F0 = F0_CHEAT_FACTOR ! Set a very small F0 to have nearly no toroidal magnetic field

  ! Initialize fields in JOREK units
  t_norm  = sqrt(mu_zero * mass_proton * central_mass * central_density * 1.d20) ! 1 jorek time unit in seconds
  qom     = real(charge) * el_chg / (mass * atomic_mass_unit)
  B0      = omega_b/qom ! In T
  Phi0    = epsilon*omega_e**2/qom/2.d0*t_norm ! In JOREK units: E_SI*t_norm
  tstep   = 1.d0


  call MPI_Comm_dup(MPI_COMM_WORLD, mpi_comm_n, ierr)
  call MPI_Comm_dup(MPI_COMM_WORLD, mpi_comm_master, ierr)

  i_tor_local = 1
  n_tor_local = 1

  call prepare_mumps_par_n0(node_list, element_list, n_tor_local, i_tor_local, mpi_comm_world, mpi_comm_n, mpi_comm_master, &
                            a_mat,  area, volume, filter=0.d0, filter_hyper=0.d0, filter_parallel=0.d0, &
                            apply_dirichlet_condition_in=apply_dirichlet , integral_weights=integral_weights )
  
  call map_matrix_to_MUMPS_datastructure(a_mat,p)

  allocate(p%rhs(p%n))

  ! Project manually
  p%JOB = 3
  p%icntl(21) = 0 ! solution is available only on host
  p%icntl(4)  = 1 ! print only errors
  ! Setup RHS by integrating manually
  p%rhs = 0.d0

  ! For U
  ! the shape of the function is fixed, and we can calculate the scale factor after
  ! projecting the function.
  call calc_rhs_f(node_list,element_list,penning_U,p%rhs)

  call DMUMPS(p)

  call broadcast_dmumps_project_struct(rank,master,p,ifail)

  do i=1,node_list%n_nodes
    do k=1,n_degrees
      index = 2*(node_list%node(i)%index(k)-1) + 1
      ! Scale JOREK fields correctly and apply F0_CHEAT_FACTOR
      ! p%rhs contains R^2 - 2 Z^2
      node_list%node(i)%values(1,k,2) = Phi0 * p%rhs(index) / F0_CHEAT_FACTOR
    enddo
  enddo

  ! For psi
    call calc_rhs_f(node_list,element_list,penning_psi,p%rhs)
  
  call DMUMPS(p)
  
  call broadcast_dmumps_project_struct(rank,master,p,ifail)

  do i=1,node_list%n_nodes
    do k=1,n_degrees
      index = 2*(node_list%node(i)%index(k)-1) + 1
      ! p^rhs contains -R^2/2
      node_list%node(i)%values(1,k,1) = B0*p%rhs(index)
    enddo
    node_list%node(i)%deltas = 0.d0
  enddo
  
  p%JOB=-2
  call DMUMPS(p)

  if(present(ifail_out)) ifail_out = ifail 
 
end subroutine jorek_penning_fields

!> E = -Grad F0*U, U = Phi0(R^2 - 2 Z^2) (see model001/initial_conditions.f90 for reference)
function penning_U(R, Z)
  real*8, intent(in) :: R, Z
  real*8 :: penning_U
  penning_U = R**2 - 2.d0*Z**2
end function penning_U

!> Projection function for psi to yield the right magnetic field.
!> B has only a z component, so F = 0, Psi(R) = -B0/2 R^2
function penning_psi(R, Z)
  real*8, intent(in) :: R, Z
  real*8 :: penning_psi
  penning_psi = -0.5d0 * R**2
end function penning_psi
end module mod_penning_case_jorek
